prompt_honai_setup() {
	zmodload zsh/datetime
	autoload -Uz vcs_info

	zstyle ':vcs_info:*' enable git cvs
	zstyle ':vcs_info:*' use-simple true
	zstyle ':vcs_info:git:*' formats '%b'
	zstyle ':vcs_info:git:*' actionformats '%b%F{yellow}!%a%f'
	zstyle ':vcs_info:git+set-message:*' hooks vcs_git_dirty vcs_git_arrow
	zstyle ':vcs_info:cvs:*' formats '%b(%s)'

	if type kubectl >/dev/null; then
		is_kubectl_found="1"
	else
		is_kubectl_found="0"
	fi

	remote_info=''
	if [[ -n "$SSH_CONNECTION" ]]; then
		remote_info='%M '
	fi

	# icons
	icon_git=''        # nf-dev-git
	icon_git_branch='' # nf-dev-git_branch
	icon_k8s='󱃾 '       # nf-md-kubernetes
	#icon_k8s='⎈ '
	icon_python='%F{blue}%f' # nf-dev-python
	icon_yj='%F{9}%f'        # nf-dev-yahoo_small
	prompt_char="❯"

	add-zsh-hook precmd prompt_honai_precmd
}

+vi-vcs_git_arrow() {
	local arrows ahead behind
	# Exit early in case the worktree is on a detached HEAD
	git rev-parse @'{u}' >/dev/null 2>&1 || return 0
	local -a ahead_and_behind=(
		$(git rev-list --left-right --count HEAD...@'{u}' 2>/dev/null)
	)
	ahead=${ahead_and_behind[1]}
	behind=${ahead_and_behind[2]}
	((ahead > 0)) && arrows+="⇡"
	((behind > 0)) && arrows+="⇣"
	hook_com[branch]+="%F{white}$arrows%f"
}

+vi-vcs_git_dirty() {
	export GIT_OPTIONAL_LOCKS=0
	local -r status="$(git status --short 2>/dev/null)"
	if [[ -n $status ]]; then
		hook_com[branch]+='%F{white}*%f'
	fi
	local -r stash_count=$(git rev-list --walk-reflogs --count refs/stash)
	if ((stash_count > 0)); then
		hook_com[branch]+='%F{white}≡%f'
	fi
}

prompt_honai_precmd() {
	unset cmd_timestamp

	vcs_info

	local prompt_infos=()

	# dir
	prompt_infos+=('%F{blue}%~%f')

	# dot indicator
	if [[ $PWD = "$HOME" && -n "$(dot status --short 2>/dev/null)" ]]; then
		prompt_infos+=("%F{red}*%f")
	fi

	# vcs
	if [[ -n ${vcs_info_msg_0_} ]]; then
		prompt_infos+=("%F{8}$vcs_info_msg_0_%f")
	fi

	# venv
	if [[ -n "$VIRTUAL_ENV" ]]; then
		local -r last="${VIRTUAL_ENV##*/}"
		# a/b/c -> b
		local -r _dir="${VIRTUAL_ENV%/*}"
		local -r last2="${_dir##*/}"
		if [[ $last =~ ^[.]?venv$ ]]; then
			local venv_name="$last2"
		else
			local venv_name="$last"
		fi
		prompt_infos+=("${icon_python} %F{8}${venv_name}%f")
	fi

	# k8s
	if [[ $is_kubectl_found = "1" ]]; then
		local -r k8s_ctx="$(kubectl config current-context)"
		prompt_infos+=("%F{8}${icon_k8s}${k8s_ctx}%f")
	fi

	PROMPT="
${remote_info}${prompt_infos[*]} %(0?..[%?] )%F{8}%*%f
%(0?.%F{blue}.%F{red})${prompt_char}%f "
}

prompt_honai_setup "$@"
